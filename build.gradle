buildscript {
    repositories {
        gradlePluginPortal()
        google()
        mavenCentral()
    }
    dependencies {
        classpath(libs.bundles.plugins)
    }
}

subprojects {
    // ./gradlew dependencyUpdates -DoutputFormatter=html
    apply plugin: "com.github.ben-manes.versions"
    // ./gradlew ktlintCheck
    // ./gradlew ktlintFormat
    apply plugin: "org.jlleitschuh.gradle.ktlint"

    ktlint {
        version = "0.45.2"
        android = true
        verbose = true
        filter {
            exclude { element ->
                element.file.path.contains("/build/generated/")
            }
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            freeCompilerArgs += "-opt-in=kotlin.RequiresOptIn"
        }
    }

    // test task for CI
    if (project.name != "shared") {
        // TODO: Currently, kmm test is not working
        afterEvaluate {
            if (project.extensions.findByName("android") != null) {
                project.tasks.register("testCI") {
                    dependsOn "testDebugUnitTest"
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
