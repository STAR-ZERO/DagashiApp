plugins {
    id("com.android.library")
    id("kotlin-android")
    id("kotlin-kapt")
    id("dagger.hilt.android.plugin")
    id("com.google.protobuf")
}

android {
    compileSdk AndroidConfigurations.COMPILE_SDK

    defaultConfig {
        minSdk AndroidConfigurations.MIN_SDK
        targetSdk AndroidConfigurations.TARGET_SDK

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion libs.versions.androidx.compose.get()
    }
    packagingOptions {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
        }
    }
}

dependencies {
    api project(":shared")

    api libs.bundles.androidx.compose

    api libs.androidx.core
    api libs.androidx.compat
    api libs.material

    implementation libs.androidx.browser

    api libs.coroutines.android

    api libs.androidx.lifecycle.viewmodel
    api libs.androidx.lifecycle.livedata

    implementation libs.hilt.android
    kapt libs.hilt.compiler

    api libs.androidx.datastore
    api libs.protobuf.java

    api libs.napier

    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.espresso

    debugImplementation libs.androidx.compose.ui.tooling
}

kapt {
    correctErrorTypes true
}

protobuf {
    protoc {
        artifact = libs.protobuf.protoc.get()
    }

    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}
